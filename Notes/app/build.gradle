import com.github.spotbugs.snom.SpotBugsTask

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'

apply from: '../../gradle_configs/common_ndk_version.gradle'
apply from: '../../gradle_configs/common_android_versions.gradle'

android {

    signingConfigs {
        release {
            storeFile file("test_only.jks")
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    defaultConfig {
        applicationId "com.serhii.apps.notes"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // App name
            manifestPlaceholders = [appLabel: "@string/app_name"]
        }

        debug {
            debuggable true
            jniDebuggable = true
            // App name
            manifestPlaceholders = [appLabel: "@string/app_name_debug"]
            applicationIdSuffix ".dev"
            // Enable test coverage
            testCoverageEnabled true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/build_ndk.mk')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

}

// SpotBugs tasks
tasks.withType(SpotBugsTask) {
    // Dependent tasks
    dependsOn 'assembleDebug'
    group = "verification"

    // Paths for .class files
    classes = fileTree("build/intermediates/javac/debug/classes/com") + fileTree("build/tmp/kotlin-classes/debug/")

    // Generate HTML report
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// SpotBug tool configuration.
// More settings: https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugsextension
// Or https://github.com/spotbugs/spotbugs-gradle-plugin
spotbugs {
    toolVersion = '3.1.10' // Possible it is not necessary
    ignoreFailures = true
    effort = 'max' // Find as much as possible issues
    reportLevel = 'low' // Report all issues
    maxHeapSize = '1g'
    reportsDir = file("$buildDir/spotbugs") // Reports directory
}

// Code coverage task
// Docs: https://docs.gradle.org/current/dsl/org.gradle.testing.jacoco.tasks.JacocoReport.html
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android library dependency
    implementation project(":LibCoreAndroid")

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.biometric:biometric:1.0.1'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.1.0'
    implementation 'androidx.navigation:navigation-ui:2.1.0'

    // JSON library - https://github.com/square/moshi
    // Stopped using it as it creates issues in release build
    // implementation("com.squareup.moshi:moshi-kotlin:1.9.1")

    // Kotlin Json lib
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    // Kotlin
    implementation "androidx.core:core-ktx:$kotlin_version"

    // ViewModel with Kotlin
    // implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$kotlin_version"
    // Fragment with Kotlin
    implementation "androidx.fragment:fragment-ktx:1.6.2"
    // Activity with Kotlin
    // implementation 'androidx.activity:activity-ktx:1.4.0'
    // Lifecycle with Kotlin
    // implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // Tests dependencies
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.0-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
