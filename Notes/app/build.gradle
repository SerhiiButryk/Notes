import com.github.spotbugs.snom.SpotBugsTask

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

apply from: '../../gradle_configs/common_ndk_version.gradle'
apply from: '../../gradle_configs/common_android_versions.gradle'

android {

    namespace = "com.serhii.apps.notes"

    buildFeatures {
        buildConfig true
        compose true
    }

    signingConfigs {
        release {
            storeFile file("test_only.jks")
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    defaultConfig {
        applicationId "com.serhii.apps.notes"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // App name
            manifestPlaceholders = [appLabel: "@string/app_name"]
        }

        debug {
            debuggable true
            jniDebuggable = true
            // App name
            manifestPlaceholders = [appLabel: "@string/app_name_debug"]
            applicationIdSuffix ".dev"
            // Enable test coverage
            testCoverageEnabled true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/build_ndk.mk')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

}

// SpotBugs tasks
// Run "./gradlew check" to execute these checks
tasks.withType(SpotBugsTask) {
    // Dependent tasks
    dependsOn 'assembleDebug'
    group = "verification"

    // Paths for .class files
    classes = fileTree("build/intermediates/javac/debug/classes/com") + fileTree("build/tmp/kotlin-classes/debug/")

    // Generate HTML report
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// SpotBug tool configuration.
// More settings: https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugsextension
// Or https://github.com/spotbugs/spotbugs-gradle-plugin
spotbugs {
    toolVersion = '3.1.10' // Possible it is not necessary
    ignoreFailures = true
    effort = 'max' // Find as much as possible issues
    reportLevel = 'low' // Report all issues
    maxHeapSize = '1g'
    reportsDir = file("$buildDir/spotbugs") // Reports directory
}

// Code coverage task
// Docs: https://docs.gradle.org/current/dsl/org.gradle.testing.jacoco.tasks.JacocoReport.html
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Internal Android library dependency
    implementation project(":LibCoreAndroid")

    // Android ui
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Kotlin Json
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    // Fragment with Kotlin
    implementation "androidx.fragment:fragment-ktx:1.8.3"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.5'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'

    // Compose
    implementation 'androidx.compose.ui:ui:1.7.1'
    implementation 'androidx.activity:activity-compose:1.9.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose-android:2.8.5'

    // Compose Material Design 3
    implementation 'androidx.compose.material3:material3:1.3.0'
    implementation "androidx.compose.material:material-icons-extended:1.7.1"

    // Compose Preview support
    debugImplementation("androidx.compose.ui:ui-tooling:1.7.1")
    debugImplementation("androidx.compose.ui:ui-tooling-preview:1.7.1")

    // Rich Text Editor
    // https://github.com/MohamedRejeb/compose-rich-editor
    implementation("com.mohamedrejeb.richeditor:richeditor-compose:1.0.0-rc06")

    // Tests dependencies
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.0-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
